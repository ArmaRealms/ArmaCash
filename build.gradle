import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '8.3.2'
    id 'java-library'
    id 'maven-publish'
}

group = 'org.black_ixx'
version = '3.2.7'

java {
    disableAutoTargetJvm()
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://nexus.hc.to/content/repositories/pub_releases' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.dakanilabs.com/repository/public' }
    maven { url = 'https://repo.maven.apache.org/maven2' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    api(libs.rosewood.rosegarden)

    compileOnly(libs.spigot.api)
    compileOnly(libs.vault)
    compileOnly(libs.treasury.api)
    compileOnly(libs.votifier)
    compileOnly(libs.placeholderapi)
    compileOnly(libs.annotations)
    compileOnly(libs.tokenmanager)
    compileOnly(libs.gamepoints)
    compileOnly(libs.nexengine)
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set("")
    minimize()

    relocate('dev.rosewood.rosegarden', 'org.black_ixx.playerpoints.libs.rosegarden')
}

build.dependsOn shadowJar
